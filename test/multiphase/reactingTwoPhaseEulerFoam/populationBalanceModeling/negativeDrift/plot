#!/usr/bin/gnuplot

set terminal postscript eps monochrome
set output "numberDensity.eps"
set decimalsign "."

set format xy '%g'
set xtics 1 mirror
set xlabel 'v(m^3)'
set ytics 0.005 mirror
set ylabel 'n(m^{-3}m^{-3})'

set xrange [1:15]
set yrange [0:1.5e-2]
set key top right

plot "<printf '4 0\n4 0.01\n6 0.01\n6 0\n'" w l t 'Kumar (2002)',\
"<printf '8 0\n8 0.01\n10 0.01\n10 0\n'" w l t 'Initial Condition',\
'box.all.numberDensity.volume.bubbles.transposed.dat' u 1:2 w lp t 'air'

set output "moments.eps"

set format xy '%g'
set xlabel 't(s)'
set ytics 0.5 mirror
set ylabel 'M_j(t)/M_j(t=0)'

set xrange [0:4]
set yrange [0:1.5]
set key top right

plot 'postProcessing/box.all.moments.volume.bubbles/0/box.all.moments.volume.bubbles.dat' u 1:($2/0.0258575) w p pt 5 t 'j=0 (Numbers), air',\
"<printf '0 1\n6 1\n'" w l t 'j=0 (Numbers), Kumar (2002)',\
'postProcessing/box.all.moments.volume.bubbles/0/box.all.moments.volume.bubbles.dat' u 1:($3/0.2325241) w p pt 6 t 'j=1 (Volume), air',\
"<printf '0 1\n6 0.33333\n'" w l t 'j=1 (Volume), Kumar (2002)'
